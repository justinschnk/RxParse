buildscript {
    repositories {
        jcenter()
        maven { url "https://jitpack.io" }
    }

    dependencies {
        classpath 'me.tatarka:gradle-retrolambda:3.2.5'
        classpath 'com.android.tools.build:gradle:2.1.0-alpha5'
        classpath 'com.github.JakeWharton:sdk-manager-plugin:-SNAPSHOT'
        classpath 'com.novoda:bintray-release:0.3.4'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.3'
        classpath 'me.tatarka.retrolambda.projectlombok:lombok.ast:0.2.3.a2'
    }

    // Exclude the version that the android plugin depends on.
    configurations.classpath.exclude group: 'com.android.tools.external.lombok'
}

apply plugin: 'com.android.library'
apply plugin: 'me.tatarka.retrolambda'

repositories {
    jcenter()
    maven { url "https://jitpack.io" }
}

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.1"

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 23
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    compile 'com.parse:parse-android:1.13.1'
    compile 'com.parse:parsefacebookutils-v4-android:1.10.4@aar'

    compile 'com.parse.bolts:bolts-tasks:1.4.0'
    compile 'com.infstory:rxbolts:1.0.3'

    compile 'io.reactivex:rxjava:1.0.7'

    compile 'com.facebook.android:facebook-android-sdk:4.9.0'
    //testCompile "org.mockito:mockito-core:1.+"
}

tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:deprecation" << "-Xlint:unchecked"
}

apply plugin: 'com.novoda.bintray-release'

def getRepositoryUsername() {
    return hasProperty('BINTRAY_USER') ? BINTRAY_USER : ""
}

def getRepositoryPassword() {
    return hasProperty('BINTRAY_KEY') ? BINTRAY_KEY : ""
}

publish {
    userOrg = 'yongjhih'
    groupId = 'com.infstory'
    artifactId = 'rxparse-facebook-v4'
    publishVersion = '2.0.1'
    desc = 'Reactive ParseFacebooUtils v4'
    website = 'https://github.com/yongjhih/RxParse'
    bintrayUser = getRepositoryUsername()
    bintrayKey = getRepositoryPassword()
    dryRun = false
}

//apply plugin: 'maven'
apply plugin: 'com.github.dcendents.android-maven'

group = 'com.github.yongjhih'

apply from: 'javadoc.gradle'
